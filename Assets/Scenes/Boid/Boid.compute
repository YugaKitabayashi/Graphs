// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Boid

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> position;
RWTexture2D<float4> velocity;

float cohesionForce;
float separationForce;
float alignmentForce;

float cohesionDistance;
float separationDistance;
float alignmentDistance;

float cohesionAngleCos;
float separationAngleCos;
float alignmentAngleCos;

float boundaryForce;
float boundaryDistance;
float minVelocity;
float maxVelocity;

float deltaTime;
uint length;

[numthreads(8,8,1)]
void Boid (uint2 id : SV_DispatchThreadID)
{
	float3 c = float3(0.0, 0.0, 0.0);
	float3 s = float3(0.0, 0.0, 0.0);
	float3 a = float3(0.0, 0.0, 0.0);

	float cn = 0;
	float an = 0;

	float3 v = velocity[id.xy];
	float3 p = position[id.xy];
	uint2 index = uint2(0, 0);
	for (; index.x < length; index.x++) {
		for (index.y = 0; index.y < length; index.y++) {
			if (id.x == index.x && id.y == index.y)continue;

			float3 direction = position[index] - p;
			float d_2 = dot(direction, direction);
			float distance = sqrt(d_2);
			float angle = dot(normalize(direction), normalize(v));

			if (distance < cohesionDistance && angle > cohesionAngleCos) {
				c += direction;
				cn++;
			}

			if (distance < separationDistance && angle > separationAngleCos) {
				s += direction / d_2;
			}

			if (distance < alignmentDistance && angle > alignmentAngleCos) {
				a += velocity[index];
				an++;
			}
		}
	}

	if (cn > 0)c *= cohesionForce / cn;
	if (an > 0)a *= alignmentForce / an;
	s *= separationForce;

	float distance = sqrt(dot(p, p));
	float3 b = -boundaryForce * p / (1.0 + exp(boundaryDistance-distance));

	v += (c + s + a + b) * deltaTime;

	distance = sqrt(dot(v, v));
	if (distance < minVelocity) {
		v *= minVelocity / distance;
		distance = minVelocity;
	}
	if (distance > maxVelocity) {
		v *= maxVelocity / distance;
		distance = maxVelocity;
	}
	velocity[id.xy] = float4(v, distance);

	p += v * deltaTime;
	distance = sqrt(dot(p, p));
	position[id.xy] = float4(p, distance);
}
